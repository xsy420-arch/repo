# Maintainer: AlphaJack <alphajack at tuta dot io>

pkgname="python-textual-fastdatatable"
_name="${pkgname#python-}"
pkgver=0.13.0
pkgrel=1
pkgdesc="A performance-focused reimplementation of Textual's DataTable widget, with a pluggable data storage backend"
arch=("any")
license=("MIT")
url="https://github.com/tconbeer/textual-fastdatatable"
depends=("python"
    "python-pandas-stubs"
    "python-pyarrow"
    "python-pytz"
    "python-textual")
makedepends=("python-build" "python-hatchling" "python-installer" "python-poetry" "python-setuptools" "python-wheel")
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name/-/_}-${pkgver}.tar.gz")
b2sums=('c5f90a5c4787a06f3654f918a8eec87dd13f161f96df028c2f59288103cec257094e116bc6bcf16125c934b3c2b016f6f224f41959084fc8402bd19ec4be1d25')

build() {
    cd "textual_fastdatatable-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "textual_fastdatatable-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
