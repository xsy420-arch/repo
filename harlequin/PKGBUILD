# Maintainer: AlphaJack <alphajack at tuta dot io>

pkgname="harlequin"
pkgver=2.2.1
pkgrel=1
pkgdesc="The SQL IDE for Your Terminal"
url="https://harlequin.sh/"
license=("MIT")
arch=("any")
depends=("python"
    "python-textual"
    "python-textual-fastdatatable"
    "python-textual-textarea"
    "python-click"
    "python-rich-click"
    "python-duckdb"
    "python-shandy-sqlfmt"
    "python-platformdirs"
    "python-pyperclip"
    "python-importlib-metadata"
    "python-tomli"
    "python-tomlkit"
    "python-questionary"
    "python-black"
    "python-ruff"
    "mypy")
makedepends=("python-build"
    "python-hatchling"
    "python-installer"
    "python-poetry"
    "python-wheel")
options=("!strip")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
b2sums=('beaf7b1943e5dee6fd42dfadc83d493d2ce6e5a3738429f51fc6db5f36e6b4c24435501b7a2a442706fac7bbb2cb19b73da1dc51b2c759b32c4af748b11bacd9')

build() {
    cd "$pkgname-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$pkgname-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
