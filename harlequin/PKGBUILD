# Maintainer: AlphaJack <alphajack at tuta dot io>

pkgname="harlequin"
pkgver=2.5.1
pkgrel=1
pkgdesc="The SQL IDE for Your Terminal"
url="https://harlequin.sh/"
license=("MIT")
arch=("any")
depends=("python"
    "python-textual"
    "python-textual-fastdatatable"
    "python-textual-textarea"
    "python-click"
    "python-rich-click"
    "python-duckdb"
    "python-shandy-sqlfmt"
    "python-platformdirs"
    "python-pyperclip"
    "python-importlib-metadata"
    "python-tomli"
    "python-tomlkit"
    "python-questionary"
    "python-black"
    "python-ruff"
    "mypy")
makedepends=("python-build"
    "python-hatchling"
    "python-installer"
    "python-poetry"
    "python-wheel")
options=("!strip")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
b2sums=('41661d02bf27f770510b5dacd58b4b481d52a4afed9adf6ee03134990e48d88b904b47ec730588b22aa45c17034bd6ca22fa79a4ee868c5426bba3751c4d65d3')

build() {
    cd "$pkgname-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$pkgname-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
