# Maintainer: Guillaume Horel <guillaume.horel@gmail.com>
pkgname='python-aenum'
_module='aenum'
pkgver='3.1.16'
pkgrel=2
pkgdesc="Advanced Enumerations (compatible with Python's stdlib Enum)"
url="https://github.com/ethanfurman/aenum"
depends=(python)
checkdepends=()
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
license=('BSD-3-Clause')
arch=('any')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
    fix-tests-for-python12.patch::https://patch-diff.githubusercontent.com/raw/ethanfurman/aenum/pull/36.patch
    https://raw.githubusercontent.com/ethanfurman/aenum/master/aenum/test_v37.py)

sha256sums=('4c7a5fb76a04ad8dc0c82b13c4cd858b755c69d16c2060a248bf3380a4d1067e'
            'd5522b85199d271f47f5b1e48ae97bac1b9a309b833acd5d6f467d94484b89c0'
            'e852ca671727e491dfa95af27fa2385a36056a306419efe3420cd34c03c9214e')

prepare() {
    cd "${_module}-${pkgver}"
    patch -p1 <../fix-tests-for-python12.patch
    rm aenum/_py2.py
}

build() {
    cd "${_module}-${pkgver}"
    python -m build -wnx
}

package() {
    cd "${_module}-${pkgver}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
    rm "${pkgdir}/${site_packages}/aenum/test_v3.py"
    install -D -m644 aenum/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

check() {
    cd "${_module}-${pkgver}"
    cp ../test_v37.py aenum/
    python -m aenum.test
}
