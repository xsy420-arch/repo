# Maintainer: xsy420 <xsy0420@gmail.com>
_pkgname=auth
pkgname=auth-cli
pkgver=0.2.0
pkgrel=1
pkgdesc="Authenticator CLI. Generate totps on the fly."
arch=('x86_64')
url="https://github.com/nnyyxxxx/auth"
license=('BSD-3-Clause')
depends=(
    'gcc-libs'
    'glibc'
    'libcrypto.so'
)
makedepends=('make' 'cmake' 'git')
source=(
    "$pkgname::git+$url.git#tag=v$pkgver"
    'catch2::git+https://github.com/catchorg/Catch2.git'
    'tomlplusplus::git+https://github.com/marzer/tomlplusplus.git'
)
sha256sums=('3719396f4fda325c9df2321e3034f7cfb18de3d505819d37e208c6d49312609e'
            'SKIP'
            'SKIP')

prepare() {
    cd "$pkgname"
    for submodule in $(git config --file .gitmodules --get-regexp path | awk '{ print $2 }'); do
        git submodule init "$submodule"
        git config "submodule.$submodule.url" "file://$srcdir/$(basename "$submodule")"
    done
    git -c protocol.file.allow=always submodule update
}

build() {
    cd "$pkgname"
    make release
}

check() {
    cd "$pkgname"
    ctest --test-dir ./build --output-on-failure
}

package() {
    cd "$pkgname"
    install -Dm 755 build/Auth "$pkgdir/usr/bin/$pkgname"
    install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
    install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm 644 "man/$_pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"
}
