# Maintainer: xsy420 <xsy0420@gmail.com>
_pkgname=auth
pkgname=auth-cli
pkgver=0.1.2
pkgrel=1
pkgdesc="Authenticator CLI. Generate totps on the fly."
arch=('x86_64')
url="https://github.com/nnyyxxxx/auth"
license=('BSD-3-Clause')
depends=('gcc-libs' 'glibc' 'openssl')
makedepends=('make' 'cmake' 'git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('e38f5463d7a71a6d258a34d067470ee49bbf3861d7f28d218c582f8b37e8020c')

prepare() {
    cd "$_pkgname-$pkgver"
    cmake --no-warn-unused-cli -DCMAKE_BUILD_TYPE:STRING=Release -DCMAKE_INSTALL_PREFIX:STRING="/usr" -S . -B ./build
}

build() {
    cd "$_pkgname-$pkgver"
    cmake --build ./build --config Release --target all -j$(nproc 2>/dev/null || getconf NPROCESSORS_CONF)
}

check() {
    cd "$_pkgname-$pkgver"
    ctest --test-dir ./build --output-on-failure
}

package() {
    cd "$_pkgname-$pkgver"
    DESTDIR="$pkgdir" cmake --install ./build
    install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
    install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    rm "$pkgdir/usr/bin/auth"
}
