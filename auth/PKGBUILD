pkgname=auth
pkgver=0.2.2
pkgrel=1
pkgdesc="Authenticator TUI. Generate totps on the fly."
arch=('x86_64')
url="https://github.com/nnyyxxxx/auth"
license=('GPL-2.0-only')
depends=('gcc-libs' 'glibc')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('744ad4e6b042277703bff9df2143cd296eafebe20a158c2c5f593a097fe5dd0a')

prepare() {
    cd "$pkgname-$pkgver"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --release --frozen
}

check() {
    cd "$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
    install -Dm 644 readme.md "$pkgdir/usr/share/doc/$pkgname/README.md"
    install -Dm 644 license "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
