# Maintainer: Andrew Sun <adsun701 at gmail dot com>

pkgname=libxls
pkgver=1.6.3
pkgrel=1
pkgdesc='Read binary Excel files from C/C++'
arch=('i686' 'x86_64')
url='https://github.com/libxls/libxls'
license=('BSD')
depends=('glibc')
source=("https://github.com/libxls/libxls/releases/download/v${pkgver}/$pkgname-$pkgver.tar.gz")
sha512sums=('003794f48f642674c90e5ad64ba9361ad7c4c6ccd3436b51bea3a6f2110629a33571d83d33e313cef0866f966bbb65046b8e7f4197b5893c8cc3a8fb4d003681')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --enable-shared \
        --disable-static \
        --program-prefix=lib # fix conflict with catdoc package
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make check || true
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
