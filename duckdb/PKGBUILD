# Maintainer: AlphaJack <alphajack at tuta dot io>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname="duckdb"
pkgver=1.4.1
pkgrel=1
pkgdesc="An in-process SQL OLAP database management system"
arch=("x86_64")
url="https://duckdb.org"
license=("MIT")
depends=(
    "gcc-libs"
    "openssl"
)
makedepends=(
    "git"
    "cmake"
)
conflicts=("duckdb-bin" "duckdb-git")
source=("$pkgname-$pkgver.tar.gz::https://github.com/duckdb/duckdb/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('1f845cb39965c9d5f5ac5005286aed233ab710d435cbbcd6ebcaaf26db797cd8d5f72e687be2591d9d427f315806fab26d9521d6e8c9335c843bd662282bcb2e')

prepare() {
    cmake \
        -S "$pkgname-$pkgver" \
        -B build \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D OVERRIDE_GIT_DESCRIBE="v${pkgver}"
}

build() {
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build

    # sqlite wrapper
    install -vDm755 -t "$pkgdir/usr/lib" build/tools/sqlite3_api_wrapper/libsqlite3_api_wrapper.so

    # license
    install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE"
}
