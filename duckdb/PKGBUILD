# Maintainer: AlphaJack <alphajack at tuta dot io>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname="duckdb"
pkgver=1.4.0
pkgrel=1
pkgdesc="An in-process SQL OLAP database management system"
arch=("x86_64")
url="https://duckdb.org"
license=("MIT")
depends=(
    "gcc-libs"
    "openssl"
)
makedepends=(
    "git"
    "cmake"
)
conflicts=("duckdb-bin" "duckdb-git")
source=("$pkgname-$pkgver.tar.gz::https://github.com/duckdb/duckdb/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('22e768dc3a0e2cb197c9ebd111a453d8a51aede61404d3c49d4e0e4507b655c675c72e506baa7b0834d49ba4687a76b2b13965051cd0d1a1cd4d442964345881')

prepare() {
    cmake \
        -S "$pkgname-$pkgver" \
        -B build \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D OVERRIDE_GIT_DESCRIBE="v${pkgver}"
}

build() {
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build

    # sqlite wrapper
    install -vDm755 -t "$pkgdir/usr/lib" build/tools/sqlite3_api_wrapper/libsqlite3_api_wrapper.so

    # license
    install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE"
}
