# Maintainer: AlphaJack <alphajack at tuta dot io>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname="duckdb"
pkgver=1.4.2
pkgrel=1
pkgdesc="An in-process SQL OLAP database management system"
arch=("x86_64")
url="https://duckdb.org"
license=("MIT")
depends=(
    "gcc-libs"
    "openssl"
)
makedepends=(
    "git"
    "cmake"
)
conflicts=("duckdb-bin" "duckdb-git")
source=("$pkgname-$pkgver.tar.gz::https://github.com/duckdb/duckdb/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('cb6ccff7c03d865db66b38fb6a1327b9db4a75b247b2321a58f30394f865e220584007ef5af007433021093d33baf4dd4d03908229c99bd9f3c895891c82c457')

prepare() {
    cmake \
        -S "$pkgname-$pkgver" \
        -B build \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D OVERRIDE_GIT_DESCRIBE="v${pkgver}"
}

build() {
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build

    # sqlite wrapper
    install -vDm755 -t "$pkgdir/usr/lib" build/tools/sqlite3_api_wrapper/libsqlite3_api_wrapper.so

    # license
    install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE"
}
