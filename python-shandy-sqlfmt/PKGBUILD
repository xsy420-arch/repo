# Maintainer: AlphaJack <alphajack at tuta dot io>

pkgname="python-shandy-sqlfmt"
_pkgname="${pkgname/python-/}"
pkgver=0.27.0
pkgrel=1
pkgdesc="An opinionated CLI tool that formats your SQL files"
url="https://pypi.org/project/shandy-sqlfmt/"
license=("Apache-2.0")
arch=("any")
depends=("python"
    "python-click"
    "python-tqdm"
    "python-platformdirs"
    "python-backports.cached_property"
    "python-tomli"
    "python-black"
    "python-gitpython"
    "flake8"
    "python-isort"
    "mypy"
)
makedepends=("python-build"
    "python-installer"
    "python-poetry"
    "python-wheel")
options=("!strip")
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname/-/_}-${pkgver}.tar.gz")
b2sums=('5a19af0fff3d54e3b19d1d2c87d248d1b43e7fe3ff98edec3186cfab9b1166b424700c1864a57e6893e1cc2a72386b9c1241c18d3fad4e2f4ae241712b6b1f30')

build() {
    cd "${_pkgname/-/_}-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "${_pkgname/-/_}-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
