# Maintainer:  Vitalii Kuzhdin <vitaliikuzhdin@gmail.com>

_name="img2unicode"
pkgname="python-${_name}"
pkgver=0.1a11
_commit="9e59b34480398d9b585572477ea840508a12ac27"
pkgrel=1
pkgdesc="A tool to display images as Unicode in your terminal"
arch=('any')
url="https://github.com/matrach/${_name}"
license=('MIT')
makedepends=('python-build' 'python-installer' 'python-wheel'
    'python-setuptools')
depends=('python' 'python-numpy' 'python-scikit-image'
    'python-pillow' 'python-scikit-learn' 'python-click' 'python-six'
    'python-matplotlib')
optdepends=('python-n2: for FastGammaRenderer'
    'python-urwid: for termview')
_pkgsrc="${_name}-${_commit}"
source=("${_pkgsrc}.tar.gz::${url}/archive/${_commit}.tar.gz")
sha256sums=('cf76d729d410be45b0ddf923111c7dc8d20766bfd26cfc4846ea11f6a8bb7a46')

build() {
    cd "${srcdir}/${_pkgsrc}"
    python -m build --wheel --no-isolation
}

package() {
    local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

    cd "${srcdir}/${_pkgsrc}"
    python -m installer --destdir="${pkgdir}" dist/*.whl

    install -Dm644 "README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
    install -d "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s "${pkgdir}${site_packages}/${_name}-${pkgver}.dist-info/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
