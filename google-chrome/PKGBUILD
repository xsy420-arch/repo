# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Knut Ahlers <knut at ahlers dot me>
# Contributor: Det <nimetonmaili g-mail>
# Contributor: t3ddy, Lex Rivera aka x-demon, ruario, Abdullah

# Check for new Linux releases in: http://googlechromereleases.blogspot.com/search/label/Stable%20updates
# or use: $ curl -sSf https://dl.google.com/linux/chrome/deb/dists/stable/main/binary-amd64/Packages | grep -A1 "Package: google-chrome-stable" | awk '/Version/{print $2}' | cut -d '-' -f1

pkgname=google-chrome
pkgver=137.0.7151.103
pkgrel=1
pkgdesc="The popular web browser by Google (Stable Channel)"
arch=('x86_64')
url="https://www.google.com/chrome"
license=('custom:chrome')
depends=(
    'alsa-lib'
    'gtk3'
    'libcups'
    'libxss'
    'libxtst'
    'nss'
    'ttf-liberation'
    'xdg-utils'
)
optdepends=(
    'pipewire: WebRTC desktop sharing under Wayland'
    'kdialog: for file dialogs in KDE'
    'gnome-keyring: for storing passwords in GNOME keyring'
    'kwallet: for storing passwords in KWallet'
)
options=('!emptydirs' '!strip')
install=$pkgname.install
_channel=stable
source=("https://dl.google.com/linux/chrome/deb/pool/main/g/google-chrome-${_channel}/google-chrome-${_channel}_${pkgver}-1_amd64.deb"
    'eula_text.html'
    "google-chrome-$_channel.sh")
sha512sums=('6f2624e931db6fcb4aea8f8f950a6344c1d9638acbf273252483879876849c382549f9ac2d19bb1b688eefca3c6e8c93c3b986b5c1b154b17b0c88e0bde9dae9'
            'fb767b0ce30c50f803d54281d61d125ef2d71ba9a1434f89ff5deded09771bb50fd5f005af64ecb619fe5bbb29cfc7691c5046786b3d712ae85ad9a85061f0fb'
            '7a8ee6af46becfeeec013b2b35eb1dc5714c04a97d8a6a4d56e4de66c58ba71ba2673331da530a93110d96bb91434f6c634e76ed7785d275664453bbcd673919')

package() {
    bsdtar -xf data.tar.xz -C "$pkgdir/"

    # Launcher
    install -m755 google-chrome-$_channel.sh "$pkgdir"/usr/bin/google-chrome-$_channel

    # Icons
    for i in 16x16 24x24 32x32 48x48 64x64 128x128 256x256; do
        install -Dm644 "$pkgdir"/opt/google/chrome/product_logo_${i/x*/}.png \
            "$pkgdir"/usr/share/icons/hicolor/$i/apps/google-chrome.png
    done

    # License
    install -Dm644 eula_text.html "$pkgdir"/usr/share/licenses/google-chrome/eula_text.html
    install -Dm644 "$pkgdir"/opt/google/chrome/WidevineCdm/LICENSE \
        "$pkgdir"/usr/share/licenses/google-chrome-$_channel/WidevineCdm-LICENSE.txt

    # Fix the Chrome desktop entry
    sed -i \
        -e "/Exec=/i\StartupWMClass=Google-chrome" \
        -e "s/x-scheme-handler\/ftp;\?//g" \
        "$pkgdir"/usr/share/applications/google-chrome.desktop

    # Remove the Debian Cron job, duplicate product logos and menu directory
    rm -r \
        "$pkgdir"/etc/cron.daily/ \
        "$pkgdir"/opt/google/chrome/cron/ \
        "$pkgdir"/opt/google/chrome/product_logo_*.{png,xpm} \
        "$pkgdir"/usr/share/menu/
}
