# Maintainer: AlphaJack <alphajack at tuta dot io>

pkgname="python-textual-textarea"
_pkgname="${pkgname/python-/}"
pkgver=0.17.0
pkgrel=1
pkgdesc="A text area (multi-line input) with syntax highlighting for Textual"
url="https://pypi.org/project/textual-textarea/"
license=("MIT")
arch=("any")
depends=("python"
    "python-textual"
    "python-pyperclip"
)
makedepends=("python-build"
    "python-hatchling"
    "python-installer"
    "python-poetry"
    "python-wheel")
options=("!strip")
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname/-/_}-${pkgver}.tar.gz")
b2sums=('311894d2339e94db429f81c8d0907b83b0ad583029dd8f2f3e844e0e1da0028cf78874c45a2da3c4b0d8f661cc09b18fec6a4c40ce761830745e186e65e9463b')

build() {
    cd "${_pkgname/-/_}-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "${_pkgname/-/_}-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
