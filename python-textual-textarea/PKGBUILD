# Maintainer: AlphaJack <alphajack at tuta dot io>

pkgname="python-textual-textarea"
_pkgname="${pkgname/python-/}"
pkgver=0.17.1
pkgrel=1
pkgdesc="A text area (multi-line input) with syntax highlighting for Textual"
url="https://pypi.org/project/textual-textarea/"
license=("MIT")
arch=("any")
depends=("python"
    "python-textual"
    "python-pyperclip"
)
makedepends=("python-build"
    "python-hatchling"
    "python-installer"
    "python-poetry"
    "python-wheel")
options=("!strip")
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname/-/_}-${pkgver}.tar.gz")
b2sums=('9c6cd19bc336547247700405c7ba86f52bfee3684774b72d8aacd07f3b1f1b95cb6f221d5bfaf36d82345908abf193bef547251436c2fc8bf32da59035c61524')

build() {
    cd "${_pkgname/-/_}-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "${_pkgname/-/_}-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
