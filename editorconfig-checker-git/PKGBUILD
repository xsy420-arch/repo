# Maintainer: xsy420 <xsy0420@gmail.com>
pkgname=editorconfig-checker-git
_pkgname=editorconfig-checker
_commit=349a4fc18f8511386df9a526221b802964deb41c
pkgver=next
pkgrel=1
pkgdesc='A tool to verify that your files are in harmony with your .editorconfig'
arch=(x86_64)
url="https://github.com/editorconfig-checker/$_pkgname"
license=(MIT)
groups=(xsy420-repo)
depends=(glibc)
makedepends=(go git)
provides=(ec)
options=(!debug)
conflicts=("$_pkgname")
source=(
	"git+${url}.git#commit=$_commit"
)
sha256sums=('b829b723caacbfbb3e7f212f94f2be5cf9916d82e2814a4a34c838d697d13716')

build() {
	cd "$srcdir/$_pkgname"
	export CGO_CPPFLAGS="$CPPFLAGS"
	export CGO_CFLAGS="$CFLAGS"
	export CGO_CXXFLAGS="$CXXFLAGS"
	export CGO_LDFLAGS="$LDFLAGS"
	#export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -trimpath -o "$_pkgname" ./cmd/...
}

check() {
	cd "$srcdir/$_pkgname"
	go test ./...
}

package() {
	cd "$srcdir/$_pkgname"
	install -Dm755 -t "$pkgdir/usr/bin/" "$_pkgname"
	ln -sf "$_pkgname" "$pkgdir/usr/bin/ec"
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
