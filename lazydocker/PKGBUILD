# shellcheck disable=SC2034,SC2154,SC2164
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Mikhail Swift <mikhail.swift@gmail.com>

pkgname=lazydocker
pkgver=0.24.4
pkgrel=1
pkgdesc='A simple terminal UI for docker and docker-compose, written in Go with the gocui library.'
arch=('x86_64')
url='https://github.com/jesseduffield/lazydocker'
license=('MIT')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jesseduffield/lazydocker/archive/v${pkgver}.tar.gz")
b2sums=('3ed6b09a4245d7975b8d20d9db7f86eefdd703bda0f77879d1ff3565ec791f96f015df532624b1cb5768041dad07f0ebfe33b4fed2fa9699862098dccfc6300e')

build() {
    cd "${pkgname}-${pkgver}"

    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    go build -o ${pkgname} \
        -ldflags "-extldflags \"${LDFLAGS}\" -s -w -X main.version=${pkgver}" main.go
}

package() {
    cd "${pkgname}-${pkgver}"

    install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
