# Maintainer: xsy420 <xsy0420@gmail.com>
pkgname=editorconfig-checker
pkgver=3.1.1
pkgrel=1
pkgdesc='A tool to verify that your files are in harmony with your .editorconfig'
arch=(x86_64)
url="https://github.com/editorconfig-checker/$pkgname"
license=(MIT)
depends=(glibc)
makedepends=(go git)
provides=(ec)
options=(!debug)
_archive="$pkgname-$pkgver"
source=("$url/archive/refs/tags/v$pkgver/$_archive.tar.gz")
sha256sums=('d6de48e949d11382bcdab68e76f6ea5e63bfc113f0132a8a888de4faf3bff569')

build() {
    cd "$_archive"
    export CGO_CPPFLAGS="$CPPFLAGS"
    export CGO_CFLAGS="$CFLAGS"
    export CGO_CXXFLAGS="$CXXFLAGS"
    export CGO_LDFLAGS="$LDFLAGS"
    export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build -trimpath -o "$pkgname" ./cmd/...
}

check() {
    cd "$_archive"
    go test ./...
}

package() {
    cd "$_archive"
    install -Dm755 -t "$pkgdir/usr/bin/" "$pkgname"
    ln -sf "$pkgname" "$pkgdir/usr/bin/ec"
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
