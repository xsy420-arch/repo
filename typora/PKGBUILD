# Maintainer: Jonas BÃ¶gle <aur@iwr.sh>
# Contributor: Jonathan Duck <duckbrain30@gmail.com>

pkgname=typora
pkgver=1.12.4
pkgrel=1
pkgdesc="A minimal markdown editor and reader."
arch=('x86_64')
license=('custom:"Copyright (c) 2015 Abner Lee All Rights Reserved."')
url="https://typora.io/"
depends=('gtk3' 'nss' 'alsa-lib')
optdepends=(
    'noto-fonts-emoji: Or some other emoji font to see emojis'
    'pandoc: Import/export for extra file formats')
_filename="${pkgname}_${pkgver}_amd64.deb"
source=("https://typora.io/linux/$_filename" "$pkgname.sh" "apply-license.crypt")
sha512sums=('f2504c5ab4c80a1b4927b5498a7d8cb15509b1485111a886add75db35984daf49485db49e0fce9ec251be76b08ddf0dc7fd1150de4b7ffd284f67db60905c47a'
            'de9c883c63f3ea35bd551c8761e605f8e1a3468943e000abcbf94bb0c5cbb5f0f6c7fa4d49ab39c177f167e0e3d0b061c861bf828627b4a34f7f1589119c3d04'
            '8da0a2c05d3161291f3f601bbfe5f6c1070a655e2d9fde7f26fd8df686140b2c3aa8ddfc3f44e62d3b653cd580a84cdf14b608506cc7df3de3fc42005b4f8894')

package() {
    # unpack archive
    bsdtar -xf data.tar.zst -C "$pkgdir/"

    # remove lintian overrides
    rm -rf "$pkgdir/usr/share/lintian/"
    # replace bin link with custom launch script
    rm -rf "$pkgdir/usr/bin/$pkgname"
    install -m755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
    # move license to correct path
    install -Dm644 "$pkgdir/usr/share/doc/$pkgname/copyright" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    # delete previous copyright path
    rm "$pkgdir/usr/share/doc/$pkgname/copyright"
    # delete doc dir if empty
    rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/doc/$pkgname" "$pkgdir/usr/share/doc"
    # remove change log from application comment
    sed -i '/Change Log/d' "$pkgdir/usr/share/applications/typora.desktop"
    # fix dir permissions
    find "$pkgdir" -type d -exec chmod 755 {} \;

    # license
    "$srcdir/apply-license.crypt" "$pkgdir"
}
