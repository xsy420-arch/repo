# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=television
_pkgname=tv
pkgver=0.13.8
pkgrel=1
pkgdesc="A general purpose fuzzy finder for your terminal"
arch=('x86_64')
url="https://github.com/alexpasmantier/television"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
checkdepends=('bat' 'fd')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('57a9457c46ea9b29591426046fd817f05d9cd4dbdad84b34ab2e62509b992ed35fd68676eeb71c1eb5fa78845612851ede8fbd0741223c66287316c1ee977620')
options=('!lto')

prepare() {
    cd "$pkgname-$pkgver"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname-$pkgver"
    cargo build --release --frozen
}

check() {
    cd "$pkgname-$pkgver"
    cargo test --frozen
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm 755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
    install -Dm 644 "man/$_pkgname.1" -t "$pkgdir/usr/share/man/man1/"
    install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
