# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=television
_pkgname=tv
pkgver=0.9.4
pkgrel=1
pkgdesc="A general purpose fuzzy finder for your terminal"
arch=('x86_64')
url="https://github.com/alexpasmantier/television"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('f4d0a500e2fa4b7e61070268ae8aaea60c1e7ead9ff6191a3557f8c70ee22e3eefedc73d9d1dcbe67553facd9497dc761d2e53d968dbc51ec6dd028a522e27b7')
options=('!lto')

prepare() {
    cd "$pkgname-$pkgver"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname-$pkgver"
    cargo build --release --frozen
}

check() {
    cd "$pkgname-$pkgver"
    cargo test --frozen
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm 755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
    install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
