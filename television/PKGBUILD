# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=television
_pkgname=tv
pkgver=0.9.3
pkgrel=1
pkgdesc="A general purpose fuzzy finder for your terminal"
arch=('x86_64')
url="https://github.com/alexpasmantier/television"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('eafea69e0af89866284dd3dcf974c07586e2c9b0da8ee3636fe48fb88bd55c0cff072ed5f09a0cce8626c0da9afd8b7e8039082036e21915107d7be5381af5e0')
options=('!lto')

prepare() {
    cd "$pkgname-$pkgver"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname-$pkgver"
    cargo build --release --frozen
}

check() {
    cd "$pkgname-$pkgver"
    cargo test --frozen
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm 755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
    install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
