# Maintainer: xsy420 <xsy0420@gmail.com>
pkgname=rofi-fcitx5
_pkgname=rofi
_commit=9d785b5ce19402d28d790e48c9d407fdcdc5aa41
pkgver=next
pkgrel=1
pkgdesc="A rofi that can use fcitx5 to change input method"
arch=(x86_64)
url="https://github.com/DaveDavenport/rofi"
license=(MIT)
groups=(xsy420-repo)
depends=(libxdg-basedir startup-notification libxkbcommon-x11 xcb-util-wm xcb-util-xrm librsvg xcb-util-cursor xcb-util-keysyms xcb-imdkit)
makedepends=(git)
optdepends=('i3-wm: use as a window switcher')
options=(!debug)
conflicts=(rofi)
source=(
	"git+${url}.git#commit=$_commit"
	"git+https://github.com/sardemff7/libgwater.git"
	"git+https://github.com/sardemff7/libnkutils.git"
)
sha256sums=('SKIP' 'SKIP' 'SKIP')

prepare() {
	cd "$_pkgname"
	git submodule init
	git config submodule.subprojects/libgwater.url subprojects/libgwater
	git config submodule.subprojects/libnkutils.url subprojects/libnkutils
	git -c protocol.file.allow=always submodule update
	sed -i "s/xfce4-terminal.wrapper/xfce4-terminal/g" script/rofi-sensible-terminal
}

build() {
	cd "$_pkgname"

	autoreconf -i
	./configure --prefix=/usr --sysconfdir=/etc --enable-imdkit
	make
}

package() {
	cd "$_pkgname"
	make install install-man DESTDIR="$pkgdir"

	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
	install -dm755 "$pkgdir/usr/share/doc/$_pkgname/examples"
	install -Dm755 Examples/*.sh "$pkgdir/usr/share/doc/$_pkgname/examples"
}
