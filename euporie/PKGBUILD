# Maintainer:  Vitalii Kuzhdin <vitaliikuzhdin@gmail.com>

pkgname="euporie"
pkgver=2.8.11
pkgrel=1
pkgdesc="Jupyter notebooks in the terminal"
arch=('any')
url="https://${pkgname}.readthedocs.io"
_url="https://github.com/joouha/${pkgname}"
license=('MIT')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-hatchling')
checkdepends=('python-pytest' 'python-pytest-asyncio')
depends=(
    # from pyproject.toml
    'python>=3.8' 'python-prompt_toolkit' 'python-pygments' 'jupyter-nbformat'
    'python-jupyter-client>=7.1' 'python-aenum' 'python-typing_extensions'
    'python-fastjsonschema' 'python-platformdirs' 'python-pyperclip'
    'python-imagesize' 'python-markdown-it-py' 'python-linkify-it-py'
    'python-mdit_py_plugins' 'python-flatlatex' 'python-timg'
    'python-pillow>=9.0' 'python-sixelcrop' 'python-universal_pathlib'
    'python-fsspec' 'python-jupytext>=1.14.0'
    # from namcap
    'python-jupyter-core' 'python-ipykernel' 'python-rich' 'python-mtable'
    'python-sympy' 'python-numpy' 'python-img2unicode' 'python-html2text'
    'python-magic' 'python-matplotlib' 'python-cairosvg' 'python-aiohttp'
    'python-ziamath' 'python-chafapy' 'python-teimpy' 'python-pylatexenc')
optdepends=('python-asyncssh: for Euporie hub support')
_pkgsrc="${pkgname}-${pkgver}"
source=("${_pkgsrc}.tar.gz::${_url}/archive/v${pkgver}.tar.gz")
sha256sums=('a0def66b9e0d064dbe2bfb4ec1af9876e19631943ed7f50742249da195f773c7')

build() {
    cd "${srcdir}/${_pkgsrc}"
    python -m build --wheel --no-isolation
}

check() {
    cd "${srcdir}/${_pkgsrc}"
    pytest
}

package() {
    local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

    cd "${srcdir}/${_pkgsrc}"
    python -m installer --destdir="${pkgdir}" dist/*.whl

    install -Dm644 "README.rst" "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
    install -d "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s "${pkgdir}${site_packages}/${_pkgsrc}.dist-info/licenses/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
